# 地球绘制

---

我们将使用算法**生成球体的顶点,纹理坐标**、**光照的基础知识**、**添加光照效果**

1. **生成球体顶点**

  ![球体顶点算法示意图](/assets/OpenGL ES入门/地球绘制/球体顶点算法示意图.png)![球体顶点算法1](/assets/OpenGL ES入门/地球绘制/球体顶点算法1.png)![球体顶点算法2](/assets/OpenGL ES入门/地球绘制/球体顶点算法2.png)

  ①由第一张图我们可以看出：一个球体可以由下到上切割成N份，组成球体。每一份的绘制由N个连续的三角形绘制成-GL\_TRIANGLE\_STRIP。

  ②由第二张图我们可以算出：球体上一个顶点在Y轴的坐标，R\_sin\(a\)。切面圆的半径=此时X轴坐标，hR=R\_cos\(a\)。

  ③由第三张图我们可以算出：球体上顶点在Z轴和X轴的坐标，Z轴上的坐标=hR\_sin\(b\)。X轴上的坐标=hR\_cos\(b\)。

  代码实现：

  ```
    @Override
    protected void initVert() {
        ArrayList<Float> vertList = new ArrayList<>();

        for (int vAngle = -90; vAngle < 90; vAngle += mVStep) {

            double hR1 = mR * Math.cos(Math.toRadians(vAngle));
            double y1 = mR * Math.sin(Math.toRadians(vAngle));

            double hR2 = mR * Math.cos(Math.toRadians(vAngle + mVStep));
            double y2 = mR * Math.sin(Math.toRadians(vAngle + mVStep));

            for (int hAngle = 0; hAngle <= 360; hAngle += mHStep) {
                double x1 = hR1 * Math.cos(Math.toRadians(hAngle));
                double z1 = hR1 * Math.sin(Math.toRadians(hAngle));

                double x2 = hR2 * Math.cos(Math.toRadians(hAngle));
                double z2 = hR2 * Math.sin(Math.toRadians(hAngle));

                vertList.add((float) x1);
                vertList.add((float) y1);
                vertList.add((float) z1);

                vertList.add((float) x2);
                vertList.add((float) y2);
                vertList.add((float) z2);
            }
        }
        float[] verts = new float[vertList.size()];
        for (int i = 0; i < vertList.size(); i++) {
            verts[i] = vertList.get(i);
        }
        mVertSize = verts.length / 3;
        mVertBuf = ShaderUtils.getFloatBuffer(verts);
    }
  ```

2. **生成纹理坐标**

  ![地球纹理坐标示意图](/assets/OpenGL ES入门/地球绘制/地球纹理坐标示意图.png)

  由图中我们可以看出只要把对应的点转成0.0-1.0，即可完成纹理坐标。公式如下：  
  s=n\_h\_step/360  
  t=n\_v\_step/180

  这样算出的纹理坐标原点为右下角，跟我们纹理坐标的左下角，我们需要再进一步转换：  
  s=1-n\_h\_step/360  
  t=n\_v\_step/180

  注：[立方体绘制](OpenGL ES入门/2. 立方体绘制.md)中，介绍了纹理坐标有可能是左上角，或右上角，需要根据实际情况转换纹理坐标原点。

  代码实现：

  ```
    @Override
    protected void initTextureCoord() {
        ArrayList<Float> texCoordList = new ArrayList<>();
        for (int i = 0; i < 180; i += mVStep) {
            float t1 = 1 - i / 180.0f;
            float t2 = 1 - (i + mVStep) / 180.0f;
            for (int j = 0; j <= 360; j += mHStep) {
                float s = 1 - j / 360.0f;

                texCoordList.add(s);
                texCoordList.add(t1);

                texCoordList.add(s);
                texCoordList.add(t2);
            }
        }
        float[] texCoords = new float[texCoordList.size()];
        for (int i = 0; i < texCoordList.size(); i++) {
            texCoords[i] = texCoordList.get(i);
        }
        mTexCoordBuf = ShaderUtils.getFloatBuffer(texCoords);
    }
  ```

3. **光照基础知识**

  一般OpenGL ES中采用phong光照模型。其计算公式为：  
    `surfaceColor = (ambient + diffuse + specular) * objectColor`  
    ①ambient：环境光  
    ②diffuse：漫反射光  
    ③specular：镜面光  
    ![phong光照](/assets/OpenGL ES入门/地球绘制/phong光照.png)

  光的计算一般都在片元着色器中，涉及的坐标都是在世界坐标系中。

  3.1 环境光计算公式：

     `ambient = ambientStrength * lightColor`  
     ①ambientStrength：环境光强度  
     ②lightColor：光颜色

     GLSL代码实现：

     ```
     /**
     * 环境光计算公式
     * @param lightColor 光源颜色
     * @param ambientStrength 环境光强度
     */
     vec4 ambient(vec4 lightColor,float ambientStrength){
         return ambientStrength * lightColor;
     }
     ```

  3.2 漫反射光计算公式：

     `diffuse = max(dot(normalize(lightPos-fragPos),N),0.0)*lightColor`  
     lightPos：光源位置  
     fragPos：顶点在世界坐标中的位置  
     N：顶点法向量\(归一化后的\)  
     lightColor：光源颜色

     如果觉得上面公式比较难记，可以按照下面方法推导。  
     ![漫反射光照](/assets/OpenGL ES入门/地球绘制/漫反射光照.png)  
     简单公式：diffuseFactor \* lightColor  
     推导：diffuseFactor = cos\(θ\)  
     ① 当cos\(θ\)&lt;0时，光源在物体平面的下方，此时光源对物体平面没有任何光照效果。max\(cos\(θ\),0.0\);  
     ② 单位化向量的点乘=向量的夹角的余弦值。cos\(θ\)=dot\(lightDir,N\);  
     ③ lightDir\(光源方向\)：lightDir=normalize\(lightPos-fragPos\);  
     ④ 顶点法向量\(地球是一个球体，所以顶点法向量=顶点向量\)， 一般顶点的法向量是由外部传入的向量。  
     外部法向量算法：\(待完成\)

     注：如果模型矩阵执行了不等比缩放，顶点的改变会导致法向量不再垂直于表面了。因此，我们不能用这样的模型矩阵来变换法向量。  
     ![不等比例缩放](/assets/OpenGL ES入门/地球绘制/不等比例缩放.png)  
     解决方法：`transpose(inverse(u_MModule)))*N`

     GLSL代码实现：

     ```
     /**
     * 漫反射光计算公式
     * @param lightColor 光源颜色
     * @param lightPos 光源位置
     * @param fragPos 顶点坐标(世界坐标)
     * @param N 顶点法线(法线为归一化后的向量)
     */
     vec4 diffuse(vec4 lightColor,vec3 lightPos,vec3 fragPos,vec3 N){
         vec3 lightDir = normalize(lightPos - fragPos);//归一化后的光照方向
         float diffuseFactor = max(dot(lightDir,N),0.0);//光照方向与法线夹角的余弦值,过滤掉比0小的值
         return diffuseFactor * lightColor;
     }
     ```

  3.3 境面光计算公式：

     `specular = specularStrength * pow(max(dot(normalize(reflect(normalize(fragPos-lightPos),N)),normalize(viewPos-fragPos)),0.0),32) * lightColor`  
      specularStrength：镜面光强度  
      fragPos：顶点在世界坐标系中的位置  
      lightPos：光源位置
      N：顶点法向量(归一化过的)  
      viewPos：摄像机\(观察点\)位置  
      32：物体的反光度。反光度越大，物体反射光的能力越强，聚光点越小。

     看到上边的公式我是晕了，没办法只能简化了。  
      ![镜面光照](/assets/OpenGL ES入门/地球绘制/镜面光照.png)  
      简单公式：specular = specularStrength \* specularFactor \* lightColor  
      ① specularFactor = pow\(cos\(θ\),32\)  
      ② 同漫反射cos\(θ\)需要大于0。cos\(θ\)=dot\(reflectDir,viewDir\)
      reflectDir：反射光方向
      viewDir：观察点方向
      ③ reflectDir = reflect(-lightDir,N)。reflect函数在[GLSL着色器语言]中有介绍
      viewDir = normalize(viewPos - fragPos);
      ④ lightDir = normalize(lightPos - fragPos);

      GLSL代码实现：
      ```
      /**
      * 镜面光计算公式
      * @param lightColor 光源颜色
      * @param lightPos 光源位置
      * @param fragPos 顶点坐标(世界坐标)
      * @param N 顶点法向量(归一化后的)
      * @param viewPos 观察点坐标
      * @param reflectDeg 反射度
      * @param specularStrength 镜面光强度
      */
      vec4 specular(vec4 lightColor,vec3 lightPos,vec3 fragPos,vec3 viewPos
                ,vec3 N,float reflectDeg,float specularStrength){
          vec3 lightDir = normalize(lightPos - fragPos);//光源方向
          vec3 reflectDir = reflect(-lightDir,N);//反射光方向
          vec3 viewDir = normalize(viewPos - fragPos);//观察点方向
          float specularFactor = pow(max(dot(reflectDir,viewDir),0.0),reflectDeg);
          return specularStrength * specularFactor * lightColor;
      }
      ```
4. **光照应用**
    
    4.1 片元着色器代码
    ```
    precision mediump float;

    varying vec2 v_texCoord;
    
    uniform sampler2D u_texSampler;
    
    varying vec3 v_lightPos;
    varying vec3 v_viewPos;
    varying vec3 v_fragPos;
    varying vec3 v_N;
    
    varying vec4 v_lightColor;
    
    /**
     * 环境光计算公式
     * @param lightColor 光源颜色
     * @param ambientStrength 环境光强度
     */
    vec4 ambient(vec4 lightColor,float ambientStrength){
        return ambientStrength * lightColor;
    }
    /**
     * 漫反射光计算公式
     * @param lightColor 光源颜色
     * @param lightPos 光源位置
     * @param fragPos 顶点坐标(世界坐标)
     * @param N 顶点法线(法线为归一化后的向量)
     */
    vec4 diffuse(vec4 lightColor,vec3 lightPos,vec3 fragPos,vec3 N){
        vec3 lightDir = normalize(lightPos - fragPos);//归一化后的光源方向
        float diffuseFactor = max(dot(lightDir,N),0.0);//光照方向与法线夹角的余弦值,过滤掉比0小的值
        return diffuseFactor * lightColor;
    }
    /**
     * 镜面光计算公式
     * @param lightColor 光源颜色
     * @param lightPos 光源位置
     * @param fragPos 顶点坐标(世界坐标)
     * @param N 顶点法向量(归一化后的)
     * @param viewPos 观察点坐标
     * @param reflectDeg 反射度
     * @param specularStrength 镜面光强度
     */
    vec4 specular(vec4 lightColor,vec3 lightPos,vec3 fragPos,vec3 viewPos
                ,vec3 N,float reflectDeg,float specularStrength){
        vec3 lightDir = normalize(lightPos - fragPos);//光源方向
        vec3 reflectDir = reflect(-lightDir,N);//反射光方向
        vec3 viewDir = normalize(viewPos - fragPos);//观察点方向
        float specularFactor = pow(max(dot(reflectDir,viewDir),0.0),reflectDeg);
        return specularStrength * specularFactor * lightColor;
    }
    
    void main() {
        vec4 ambientColor = ambient(v_lightColor,0.1);
        vec4 diffuseColor = diffuse(v_lightColor,v_lightPos,v_fragPos,v_N);
        vec4 specularColor = specular(v_lightColor,v_lightPos,v_fragPos,v_viewPos,v_N,32.0,0.5);
    	vec4 texColor = texture2D(u_texSampler,v_texCoord);
    	gl_FragColor = (ambientColor + diffuseColor + specularColor) * texColor;
    }
    ```
    4.2 顶点着色器代码：
    ```
    attribute vec3 a_position;
    attribute vec2 a_texCoord;
    attribute vec3 a_lightPos;//光源位置
    attribute vec3 a_viewPos;//观察点位置
    
    uniform mat4 u_MMatrix;
    uniform mat4 u_VMatrix;
    uniform mat4 u_ProjMatrix;
    
    varying vec2 v_texCoord;
    
    varying vec3 v_lightPos;
    varying vec3 v_viewPos;
    varying vec3 v_fragPos;
    varying vec3 v_N;
    
    varying vec4 v_lightColor;
    void main() {
    	gl_Position = u_ProjMatrix * u_VMatrix * u_MMatrix * vec4(a_position,1.0);
    	v_texCoord = a_texCoord;
    	
    	v_lightPos = a_lightPos;
    	v_viewPos = a_viewPos;
    	v_fragPos = (u_MMatrix * vec4(a_position,1.0)).xyz;
    	
    	mat4 nMatrix = transpose(inverse(u_MMatrix));//法向量矩阵
    	//法向量
    	v_N = normalize((nMatrix * vec4(a_position,1.0)).xyz);
    }
    ```